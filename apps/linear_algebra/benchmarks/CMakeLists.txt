add_executable(halide_benchmarks halide_benchmarks.cpp)
target_compile_definitions(halide_benchmarks PRIVATE ENABLE_FTZ_DAZ)
target_link_libraries(halide_benchmarks PRIVATE halide_blas Halide::Runtime Halide::Tools)
set(BENCHMARK_TARGETS halide_benchmarks)

find_package(Eigen3)
if (TARGET Eigen3::Eigen)
    add_executable(eigen_benchmarks eigen_benchmarks.cpp)
    target_compile_definitions(eigen_benchmarks PRIVATE EIGEN_DONT_PARALLELIZE ENABLE_FTZ_DAZ)
    target_link_libraries(eigen_benchmarks PRIVATE Eigen3::Eigen Halide::Tools)
    list(APPEND BENCHMARK_TARGETS eigen_benchmarks)
endif ()

foreach (BLAS_TARGET IN LISTS BLAS_TARGETS)
    set(TARGET ${BLAS_TARGET}_benchmarks)
    add_executable(${TARGET} cblas_benchmarks.cpp)
    target_link_libraries(${TARGET} PRIVATE BLAS::${BLAS_TARGET} Halide::Tools)
    list(APPEND BENCHMARK_TARGETS ${TARGET})
endforeach ()

# Large powers of two are a pathological case for the cache, so avoid
# them for the benchmarks.
set(BLAS_LEVELS L1 L2 L3)
list(APPEND L1_BENCHMARK_SIZES 16 64 288 1056 2080)
list(APPEND L2_BENCHMARK_SIZES 32 64 128 288 544 1056 2080)
list(APPEND L3_BENCHMARK_SIZES 32 64 128 288 544 1056 2080)
list(APPEND L1_BENCHMARKS scopy dcopy sscal dscal saxpy daxpy sdot ddot sasum dasum)
list(APPEND L2_BENCHMARKS sgemv_notrans dgemv_notrans sgemv_trans dgemv_trans sger dger)
list(APPEND L3_BENCHMARKS sgemm_notrans dgemm_notrans sgemm_transA dgemm_transA sgemm_transB dgemm_transB sgemm_transAB dgemm_transAB)

foreach (TARGET IN LISTS BENCHMARK_TARGETS)
    string(REPLACE "_benchmarks" "" BLA_VENDOR "${TARGET}")
    foreach (LEVEL IN LISTS BLAS_LEVELS)
        foreach (FUNC IN LISTS ${LEVEL}_BENCHMARKS)
            foreach (SIZE IN LISTS ${LEVEL}_BENCHMARK_SIZES)
                set(TEST_NAME ${BLA_VENDOR}_${FUNC}_${SIZE})
                add_test(NAME ${TEST_NAME}
                         COMMAND ${TARGET} ${FUNC} ${SIZE})
                set_tests_properties("${TEST_NAME}" PROPERTIES LABELS "${BLA_VENDOR};${LEVEL}")
            endforeach ()
        endforeach ()
    endforeach ()
endforeach ()